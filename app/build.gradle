buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.7.0'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def findByIpProperties = new Properties()
findByIpProperties.load(new FileInputStream(rootProject.file("findbyip.properties")))

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.guliash.findbyip"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resValue "string", "google_maps_api_key", (project.findProperty("googleMapsApiKey") ?: "")

    }
    signingConfigs {
        release {
            keyAlias findByIpProperties['keyAlias']
            keyPassword findByIpProperties['keyPassword']
            storeFile file(findByIpProperties['storeFile'])
            storePassword findByIpProperties['storePassword']
        }
    }
    lintOptions {
        abortOnError true
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions 'ip'

    productFlavors {
        freegeoip {
            dimension 'ip'
        }
        geoip {
            dimension 'ip'
        }
    }

    sourceSets {
        androidTest {
            java.srcDir 'src/testCommon/java'
        }
        test {
            java.srcDir 'src/testCommon/java'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

apply from: './dependencies.gradle'